/**
 * \file
 * \brief Defines a parser class that reads an SSL specification and returns
 *        the list of SSL instruction and table definitions.
 *
 * \authors
 * Copyright (C) 1997, Shane Sendall
 * \authors
 * Copyright (C) 1998-2001, The University of Queensland
 * \authors
 * Copyright (C) 1998, David Ung
 * \authors
 * Copyright (C) 2001, Sun Microsystems, Inc
 * \authors
 * Copyright (C) 2014-2016, Kyle Guinn
 *
 * \copyright
 * See the file "LICENSE.TERMS" for information on usage and redistribution of
 * this file, and for a DISCLAIMER OF ALL WARRANTIES.
 */

%name SSLParser

%{
	#ifdef HAVE_CONFIG_H
	#include <config.h>
	#endif
%}

%header{
	#include "exp.h"
	#include "insnameelem.h"
	#include "operator.h"
	#include "rtl.h"
	#include "statement.h"
	#include "table.h"
	#include "type.h"
	#include "types.h"

	class SSLScanner;
%}

%union {
	Exp            *exp;
	OPER            op;
	std::string    *str;
	int             num;
	double          dbl;
	Statement      *regtransfer;
	Type           *typ;

	Table          *tab;
	InsNameElem    *insel;
	std::list<std::string>   *parmlist;
	std::list<std::string>   *strlist;
	std::deque<OPER>         *oplist;
	std::deque<Exp *>        *exprlist;
	std::deque<std::string>  *namelist;
	std::list<Exp *>         *explist;
	RTL            *rtlist;
	bool            endianness;
}

%{
	#include "sslscanner.h"

	#ifdef GARBAGE_COLLECTOR
	#include <gc/gc.h>
	#endif

	#include <algorithm>
	#include <sstream>
	#include <string>

	#include <cstdlib>
	#include <cassert>

	OPER strToTerm(const char *s);                 // Convert string to a Terminal (if possible)
	Exp *listExpToExp(std::list<Exp *> *le);       // Convert a STL list of Exp* to opList
	Exp *listStrToExp(std::list<std::string> *ls); // Convert a STL list of strings to opList
%}

%define DEBUG 1

// %define INHERIT : public gc  // This is how to force the parser class to be declared as derived from class gc

%define MEMBERS \
	public: \
		virtual ~SSLParser(); \
		static Statement *parseExp(const char *str); /* Parse an expression or assignment from a string */ \
		/* The code for expanding tables and saving to the dictionary */ \
		void expandTables(InsNameElem *iname, std::list<std::string> *params, RTL *o_rtlist, RTLInstDict &Dict); \
		Exp *makeSuccessor(Exp *e); /* Get successor (of register expression) */ \
	\
		/* \
		 * The scanner. \
		 */ \
		SSLScanner *theScanner; \
	protected: \
	\
		/* \
		 * Result for parsing an assignment. \
		 */ \
		Statement *the_asgn; \
	\
		/* \
		 * Maps SSL constants to their values. \
		 */ \
		std::map<std::string, int> ConstTable; \
	\
		/* \
		 * maps index names to instruction name-elements \
		 */ \
		std::map<std::string, const InsNameElem *> indexrefmap; \
	\
		/* \
		 * Maps table names to Table's.\
		 */ \
		std::map<std::string, Table *> TableDict; \
	\
		/* \
		 * True when FLOAT keyword seen; false when INTEGER keyword seen \
		 * (in @REGISTER section) \
		 */ \
		bool bFloat = false;

%define CONSTRUCTOR_PARAM \
	std::istream &in, bool trace
%define CONSTRUCTOR_INIT \
	: theScanner(new SSLScanner(in, trace))
%define CONSTRUCTOR_CODE \
	if (trace) yydebug = 1; else yydebug = 0;

%define PARSE_PARAM RTLInstDict &Dict

/*==============================================================================
 * Declaration of token types, associativity and precedence
 *============================================================================*/

%token <op>  LOG_OP COND_OP BIT_OP ARITH_OP FARITH_OP
%token <op>  FPUSH FPOP NOT LNOT FNEG S_E
%token <op>  ADDR CONV_FUNC TRUNC_FUNC FABS_FUNC TRANSCEND
%token <str> NAME ASSIGNTYPE
%token <str> REG_ID DECOR
%token <str> TEMP
%token <str> NAME_CALL NAME_LOOKUP

%token       ENDIANNESS BIG LITTLE
%token       COVERS INDEX
%token       SHARES THEN LOOKUP_RDC
%token       ASSIGN TO AT REG_IDX EQUATE
%token       MEM_IDX TOK_INTEGER TOK_FLOAT FAST OPERAND
%token       FETCHEXEC CAST_OP FLAGMACRO SUCCESSOR

%token <num> NUM REG_NUM
%token <dbl> FLOATNUM       // I'd prefer type double here!

%token

%left LOG_OP
%right COND_OP
%left BIT_OP
%left ARITH_OP
%left FARITH_OP
%right NOT LNOT FCHS
%left CAST_OP
%left LOOKUP_RDC
%left S_E               // Sign extend. Note it effectively has low precedence, because being a post operator,
                        // the whole expression is already parsed, and hence is sign extended.
                        // Another reason why ! is deprecated!
%nonassoc AT

%type <exp> exp location exp_term
%type <op> bin_oper
%type <str> param
%type <regtransfer> rt assign_rt
%type <typ> assigntype
%type <num> cast
%type <tab> table_expr
%type <insel> name_contract instr_elem instr_name
%type <strlist> reg_table
%type <parmlist> list_parameter func_parameter
%type <namelist> name_expand str_term str_expr str_array
%type <explist> flag_list
%type <oplist> opstr_array opstr_expr
%type <exprlist> exprstr_array exprstr_expr
%type <explist> list_actualparameter
%type <rtlist> rt_list
%type <endianness> esize

%%

specorasgn
	: assign_rt { the_asgn = $1; }
	| exp       { the_asgn = new Assign(new Terminal(opNil), $1); }
	| specification
	;

specification
	: specification parts ';'
	| parts ';'
	;

parts
	: instr
	| FETCHEXEC rt_list { Dict.fetchExecCycle = $2; }

	/* Name := value */
	| constants
	| table_assign

	/* Optional one-line section declaring endianness */
	| endianness

	/* Optional section describing faster versions of instructions (e.g. that don't inplement the full
	 * specifications, but if they work, will be much faster) */
	| fastlist

	/* Definitions of registers (with overlaps, etc) */
	| reglist

	/* Declaration of "flag functions". These describe the detailed flag setting semantics for insructions */
	| flag_fnc

	/* Addressing modes (or instruction operands) (optional) */
	| OPERAND operandlist { Dict.fixupParams(); }
	;

operandlist
	: operandlist ',' operand
	| operand
	;

operand
	/* In the .tex documentation, this is the first, or variant kind
	 * Example: reg_or_imm := { imode, rmode }; */
	: param EQUATE '{' list_parameter '}' {
		// Note: the below copies the list of strings!
		Dict.DetParamMap[*$1].params = *$4;
		Dict.DetParamMap[*$1].kind = PARAM_VARIANT;
		delete $1;
		//delete $4;
	  }

	/* In the documentation, these are the second and third kinds
	 * The third kind is described as the functional, or lambda, form
	 * In terms of DetParamMap[].kind, they are PARAM_EXP unless there
	 * actually are parameters in square brackets, in which case it is
	 * PARAM_LAMBDA
	 * Example: indexA  rs1, rs2 *i32* r[rs1] + r[rs2] */
	| param list_parameter func_parameter assigntype exp {
		ParamEntry &param = Dict.DetParamMap[*$1];
		Statement *asgn = new Assign($4, new Terminal(opNil), $5);
		// Note: The below 2 copy lists of strings (to be deleted below!)
		param.params = *$2;
		param.funcParams = *$3;
		param.asgn = asgn;
		param.kind = PARAM_ASGN;

		if (!param.funcParams.empty())
			param.kind = PARAM_LAMBDA;
		delete $1;
		//delete $2;
		//delete $3;
	  }
	;

func_parameter
	: '[' list_parameter ']' { $$ = $2; }
	| /* empty */            { $$ = new std::list<std::string>; }
	;

reglist
	: TOK_INTEGER { bFloat = false; } a_reglists
	| TOK_FLOAT   { bFloat = true; }  a_reglists
	;

a_reglists
	: a_reglists ',' a_reglist
	| a_reglist
	;

a_reglist
	: REG_ID INDEX NUM {
		if (Dict.RegMap.count(*$1))
			yyerror("Name reglist declared twice");
		Dict.RegMap[*$1] = $3;
		delete $1;
	  }

	| REG_ID '[' NUM ']' INDEX NUM {
		if (Dict.RegMap.count(*$1))
			yyerror("Name reglist declared twice");
		Dict.addRegister(*$1, $6, $3, bFloat);
		delete $1;
	  }

	| REG_ID '[' NUM ']' INDEX NUM COVERS REG_ID TO REG_ID {
		if (Dict.RegMap.count(*$1))
			yyerror("Name reglist declared twice");
		Dict.RegMap[*$1] = $6;
		// Now for detailed Reg information
		if (Dict.DetRegMap.count($6))
			yyerror("Index used for more than one register");
		Dict.DetRegMap[$6].s_name(*$1);
		Dict.DetRegMap[$6].s_size($3);
		Dict.DetRegMap[$6].s_address(nullptr);
		// check range is legitimate for size. 8,10
		if (!Dict.RegMap.count(*$8)
		 || !Dict.RegMap.count(*$10)) {
			yyerror("Undefined range");
		} else {
			int bitsize = Dict.DetRegMap[Dict.RegMap[*$10]].g_size();
			for (int i = Dict.RegMap[*$8]; i != Dict.RegMap[*$10]; ++i) {
				if (!Dict.DetRegMap.count(i)) {
					yyerror("Not all registers in range defined");
					break;
				}
				bitsize += Dict.DetRegMap[i].g_size();
				if (bitsize > $3) {
					yyerror("Range exceeds size of register");
					break;
				}
			}
			if (bitsize < $3) {
				yyerror("Register size is exceeds registers in range");
				// copy information
			}
		}
		Dict.DetRegMap[$6].s_mappedIndex(Dict.RegMap[*$8]);
		Dict.DetRegMap[$6].s_mappedOffset(0);
		Dict.DetRegMap[$6].s_float(bFloat);
		delete $1;
		delete $8;
		delete $10;
	  }

	| REG_ID '[' NUM ']' INDEX NUM SHARES REG_ID AT '[' NUM TO NUM ']' {
		if (Dict.RegMap.count(*$1))
			yyerror("Name reglist declared twice");
		Dict.RegMap[*$1] = $6;
		// Now for detailed Reg information
		if (Dict.DetRegMap.count($6))
			yyerror("Index used for more than one register");
		Dict.DetRegMap[$6].s_name(*$1);
		Dict.DetRegMap[$6].s_size($3);
		Dict.DetRegMap[$6].s_address(nullptr);
		// Do checks
		if ($3 != ($13 - $11) + 1)
			yyerror("Size does not equal range");
		if (Dict.RegMap.count(*$8)) {
			if ($13 >= Dict.DetRegMap[Dict.RegMap[*$8]].g_size())
				yyerror("Range extends over target register");
		} else {
			yyerror("Shared index not yet defined");
		}
		Dict.DetRegMap[$6].s_mappedIndex(Dict.RegMap[*$8]);
		Dict.DetRegMap[$6].s_mappedOffset($11);
		Dict.DetRegMap[$6].s_float(bFloat);
		delete $1;
		delete $8;
	  }

	| '[' reg_table ']' '[' NUM ']' INDEX NUM TO NUM {
		if ((int)$2->size() != ($10 - $8 + 1)) {
			std::cerr << "size of register array does not match mapping to r[" << $8 << ".." << $10 << "]\n";
			exit(1);
		} else {
			auto loc = $2->begin();
			for (int x = $8; x <= $10; ++x, ++loc) {
				if (Dict.RegMap.count(*loc))
					yyerror("Name reglist declared twice");
				Dict.addRegister(*loc, x, $5, bFloat);
			}
			delete $2;
		}
	  }

	| '[' reg_table ']' '[' NUM ']' INDEX NUM {
		for (auto loc = $2->begin(); loc != $2->end(); ++loc) {
			if (Dict.RegMap.count(*loc))
				yyerror("Name reglist declared twice");
			Dict.addRegister(*loc, $8, $5, bFloat);
		}
		delete $2;
	  }
	;

reg_table
	: reg_table ',' REG_ID { $$ = $1; $$->push_back(*$3); delete $3; }
	| REG_ID   { $$ = new std::list<std::string>(1, *$1); delete $1; }
	;

/* Flag definitions */
flag_fnc
	: NAME_CALL list_parameter ')' '{' rt_list '}' {
		Dict.FlagFuncs[*$1] = new FlagDef(listStrToExp($2), $5);
		delete $1;
		//delete $2;
	  }
	;

constants
	: NAME EQUATE NUM {
		if (ConstTable.count(*$1))
			yyerror("Constant declared twice");
		ConstTable[*$1] = $3;
		delete $1;
	  }

	| NAME EQUATE NUM ARITH_OP NUM {
		if (ConstTable.count(*$1))
			yyerror("Constant declared twice");
		else if ($4 == opMinus)
			ConstTable[*$1] = $3 - $5;
		else if ($4 == opPlus)
			ConstTable[*$1] = $3 + $5;
		else
			yyerror("Constant expression must be NUM + NUM or NUM - NUM");
		delete $1;
	  }
	;


table_assign
	: NAME EQUATE table_expr { TableDict[*$1] = $3; delete $1; }
	;

table_expr
	: str_expr     { $$ = new NameTable(*$1); delete $1; }
	| opstr_expr   { $$ = new OpTable(*$1);   delete $1; }
	| exprstr_expr { $$ = new ExprTable(*$1); delete $1; }
	;

str_expr
	: str_expr str_term {
		// cross-product of two str_expr's
		$$ = new std::deque<std::string>;
		for (auto i = $1->begin(); i != $1->end(); ++i)
			for (auto j = $2->begin(); j != $2->end(); ++j)
				$$->push_back((*i) + (*j));
		delete $1;
		delete $2;
	  }
	| str_term { $$ = $1; }
	;

str_term
	: '{' str_array '}' { $$ = $2; }
	| name_expand       { $$ = $1; }
	;

str_array
	: str_array ',' str_expr {
		$$ = $1;
		$$->insert($$->end(), $3->begin(), $3->end());  // append $3
		delete $3;
	  }
	| str_array ',' '"' '"' { $1->push_back(""); }
	| str_expr              { $$ = $1; }
	;

name_expand
	: '\'' NAME '\'' {
		$$ = new std::deque<std::string>;
		$$->push_back("");
		$$->push_back(*$2);
		delete $2;
	  }

	| '"' NAME '"' {
		$$ = new std::deque<std::string>(1, *$2);
		delete $2;
	  }

	| '$' NAME {
		// expand $2 from table of names
		auto it = TableDict.find(*$2);
		if (it == TableDict.end()) {
			std::ostringstream o;
			o << "Could not dereference name " << *$2;
			yyerror(o.str().c_str());
		} else if (it->second->getType() != NAMETABLE) {
			std::ostringstream o;
			o << "Name " << *$2 << " is not a NAMETABLE";
			yyerror(o.str().c_str());
		} else {
			$$ = new std::deque<std::string>(((NameTable *)it->second)->records);
		}
		delete $2;
	  }

	| NAME {
		// try and expand $1 from table of names. if fail, expand using '"' NAME '"' rule
		auto it = TableDict.find(*$1);
		if (it == TableDict.end()) {
			$$ = new std::deque<std::string>(1, *$1);
		} else if (it->second->getType() != NAMETABLE) {
			std::ostringstream o;
			o << "Name " << *$1 << " is not a NAMETABLE";
			yyerror(o.str().c_str());
		} else {
			$$ = new std::deque<std::string>(((NameTable *)it->second)->records);
		}
		delete $1;
	  }
	;

/* Example: OP2 := { "<<",  ">>",  ">>A" }; */
opstr_expr
	: '{' opstr_array '}' { $$ = $2; }
	;

opstr_array
	: opstr_array ',' '"' bin_oper '"' { $$ = $1; $$->push_back($4); }
	| '"' bin_oper '"'           { $$ = new std::deque<OPER>(1, $2); }
	;

bin_oper
	: BIT_OP    { $$ = $1; }
	| ARITH_OP  { $$ = $1; }
	| FARITH_OP { $$ = $1; }
	;

/* Example: COND1_C := { "~%ZF", "%ZF", "~(%ZF | (%NF ^ %OF))", ... }; */
exprstr_expr
	: '{' exprstr_array '}' { $$ = $2; }
	;

exprstr_array
	: exprstr_array ',' '"' exp '"' { $$ = $1; $$->push_back($4); }
	| '"' exp '"'            { $$ = new std::deque<Exp *>(1, $2); }
	;

instr
	: instr_name { $1->getrefmap(indexrefmap); } list_parameter rt_list {
		// This function expands the tables and saves the expanded RTLs to the dictionary
		expandTables($1, $3, $4, Dict);
		indexrefmap.clear();
	  }
	;

instr_name
	: instr_elem { $$ = $1; }
	| instr_name DECOR {
		// remove all . from the decoration
		$2->erase(std::remove($2->begin(), $2->end(), '.'), $2->end());

		$$ = $1;
		$$->append(new InsNameElem(*$2));
		delete $2;
	  }
	;

instr_elem
	: NAME                     { $$ = new InsNameElem(*$1); delete $1; }
	| name_contract            { $$ = $1; }
	| instr_elem name_contract { $$ = $1; $$->append($2); }
	;

name_contract
	: '\'' NAME '\'' { $$ = new InsOptionElem(*$2); delete $2; }

	| '"' NAME '"' { $$ = new InsNameElem(*$2); delete $2; }

	| NAME_LOOKUP NUM ']' {
		auto it = TableDict.find(*$1);
		if (it == TableDict.end()) {
			std::ostringstream o;
			o << "Table " << *$1 << " has not been declared";
			yyerror(o.str().c_str());
		} else if (it->second->getType() != NAMETABLE) {
			std::ostringstream o;
			o << "Name " << *$1 << " is not a NAMETABLE";
			yyerror(o.str().c_str());
		} else if (($2 < 0) || ($2 >= (int)((NameTable *)it->second)->records.size())) {
			std::ostringstream o;
			o << "Can't get element " << $2 << " of table " << *$1;
			yyerror(o.str().c_str());
		} else {
			$$ = new InsNameElem(((NameTable *)it->second)->records[$2]);
		}
		delete $1;
	  }

	| '$' NAME_LOOKUP NUM ']' {
		auto it = TableDict.find(*$2);
		if (it == TableDict.end()) {
			std::ostringstream o;
			o << "Table " << *$2 << " has not been declared";
			yyerror(o.str().c_str());
		} else if (it->second->getType() != NAMETABLE) {
			std::ostringstream o;
			o << "Name " << *$2 << " is not a NAMETABLE";
			yyerror(o.str().c_str());
		} else if (($3 < 0) || ($3 >= (int)((NameTable *)it->second)->records.size())) {
			std::ostringstream o;
			o << "Can't get element " << $3 << " of table " << *$2;
			yyerror(o.str().c_str());
		} else {
			$$ = new InsNameElem(((NameTable *)it->second)->records[$3]);
		}
		delete $2;
	  }

	/* Example: ARITH[IDX]  where ARITH := { "ADD", "SUB", ... }; */
	| NAME_LOOKUP NAME ']' {
		auto it = TableDict.find(*$1);
		if (it == TableDict.end()) {
			std::ostringstream o;
			o << "Table " << *$1 << " has not been declared";
			yyerror(o.str().c_str());
		} else if (it->second->getType() != NAMETABLE) {
			std::ostringstream o;
			o << "Name " << *$1 << " is not a NAMETABLE";
			yyerror(o.str().c_str());
		} else {
			$$ = new InsListElem(*$1, (NameTable *)it->second, *$2);
		}
		delete $1;
		delete $2;
	  }

	| '$' NAME_LOOKUP NAME ']' {
		auto it = TableDict.find(*$2);
		if (it == TableDict.end()) {
			std::ostringstream o;
			o << "Table " << *$2 << " has not been declared";
			yyerror(o.str().c_str());
		} else if (it->second->getType() != NAMETABLE) {
			std::ostringstream o;
			o << "Name " << *$2 << " is not a NAMETABLE";
			yyerror(o.str().c_str());
		} else {
			$$ = new InsListElem(*$2, (NameTable *)it->second, *$3);
		}
		delete $2;
		delete $3;
	  }
	;

rt_list
	: rt_list rt {
		// append any automatically generated register transfers and clear the list they were stored in.
		// Do nothing for a NOP (i.e. $2 == nullptr)
		$$ = $1;
		if ($2)
			$$->appendStmt($2);
	  }
	| rt {
		$$ = new RTL();
		if ($1)
			$$->appendStmt($1);
	  }
	;

rt
	: assign_rt { $$ = $1; }

	/* Example: ADDFLAGS(r[tmp], reg_or_imm, r[rd]) */
	| NAME_CALL list_actualparameter ')' {
		if (Dict.FlagFuncs.count(*$1)) {
			// Note: SETFFLAGS assigns to the floating point flags. All others to the integer flags
			bool bFloat = *$1 == "SETFFLAGS";
			OPER op = bFloat ? opFflags : opFlags;
			$$ = new Assign(new Terminal(op), new Binary(opFlagCall,
			                                             new Const($1->c_str()),
			                                             listExpToExp($2)));
			//delete $1;
		} else {
			std::ostringstream o;
			o << *$1 << " is not declared as a flag function";
			yyerror(o.str().c_str());
			delete $1;
		}
		delete $2;
	  }

	| FLAGMACRO flag_list ')' { $$ = nullptr; }

	/* E.g. undefineflags() (but we don't handle this yet... flags are changed, but not in a way we support) */
	| FLAGMACRO ')' { $$ = nullptr; }
	| '_'           { $$ = nullptr; }
	;

flag_list
	: flag_list ',' REG_ID {
		// Not sure why the below is commented out (MVE)
		/*Location *pFlag = Location::regOf(Dict.RegMap[*$3]);
		$1->push_back(pFlag);
		$$ = $1;*/
		$$ = nullptr;
		delete $3;
	  }
	| REG_ID {
		/*auto tmp = new std::list<Exp *>;
		auto pFlag = new Unary(opIdRegOf, Dict.RegMap[*$1]);
		tmp->push_back(pFlag);
		$$ = tmp;*/
		$$ = nullptr;
		delete $1;
	  }
	;

/* Note: this list is a list of strings (other code needs this) */
list_parameter
	: list_parameter ',' param { $$ = $1; $$->push_back(*$3); delete $3; }
	| param        { $$ = new std::list<std::string>(1, *$1); delete $1; }
	| /* empty */  { $$ = new std::list<std::string>; }
	;

param
	: NAME {
		Dict.ParamSet.insert(*$1);  // MVE: Likely wrong. Likely supposed to be OPERAND params only
		$$ = $1;
	  }
	;

list_actualparameter
	: list_actualparameter ',' exp { $$ = $1; $$->push_back($3); }
	| exp                    { $$ = new std::list<Exp *>(1, $1); }
	| /* empty */            { $$ = new std::list<Exp *>; }
	;

assign_rt
	/*   Size   guard =>    lhs      :=   rhs */
	: assigntype exp THEN location EQUATE exp {
		auto a = new Assign($1, $4, $6);
		a->setGuard($2);
		$$ = a;
	  }
	/*   Size      lhs      :=   rhs */
	| assigntype location EQUATE exp {
		// update the size of any generated RT's
		$$ = new Assign($1, $2, $4);
	  }

	/* FPUSH and FPOP are special "transfers" with just a Terminal */
	| FPUSH { $$ = new Assign(new Terminal(opNil), new Terminal($1)); }
	| FPOP  { $$ = new Assign(new Terminal(opNil), new Terminal($1)); }

	/* Just a RHS? Is this used? Note: flag calls are handled at the rt: level */
	| assigntype exp { $$ = new Assign($1, nullptr, $2); }
	;

exp_term
	: NUM                         { $$ = new Const($1); }
	| FLOATNUM                    { $$ = new Const($1); }
	| '(' exp ')'                 { $$ = $2; }
	| location                    { $$ = $1; }
	| '[' exp '?' exp ':' exp ']' { $$ = new Ternary(opTern, $2, $4, $6); }

	/* Address-of, for LEA type instructions */
	| ADDR exp ')' { $$ = new Unary($1, $2); }

	/* Conversion functions, e.g. fsize(32, 80, modrm). Args are FROMsize, TOsize, EXPression */
	| CONV_FUNC NUM ',' NUM ',' exp ')' { $$ = new Ternary($1, new Const($2), new Const($4), $6); }

	/* Truncation function: ftrunc(3.01) == 3.00 */
	| TRUNC_FUNC exp ')' { $$ = new Unary($1, $2); }

	/* fabs function: fabs(-3.01) == 3.01 */
	| FABS_FUNC exp ')' { $$ = new Unary($1, $2); }

	/* FPUSH and FPOP */
	| FPUSH { $$ = new Terminal($1); }
	| FPOP  { $$ = new Terminal($1); }

	/* Transcendental functions */
	| TRANSCEND exp ')' { $$ = new Unary($1, $2); }

	/* Example: *Use* of COND[idx] */
	| NAME_LOOKUP NAME ']' {
		auto ir = indexrefmap.find(*$2);
		auto it = TableDict.find(*$1);
		if (ir == indexrefmap.end()) {
			std::ostringstream o;
			o << "Index " << *$2 << " not declared for use";
			yyerror(o.str().c_str());
		} else if (it == TableDict.end()) {
			std::ostringstream o;
			o << "Table " << *$1 << " not declared for use";
			yyerror(o.str().c_str());
		} else if (it->second->getType() != EXPRTABLE) {
			std::ostringstream o;
			o << "Table " << *$1 << " is not an expression table but appears to be used as one";
			yyerror(o.str().c_str());
		} else if ((int)((ExprTable *)it->second)->expressions.size() < ir->second->ntokens()) {
			std::ostringstream o;
			o << "Table " << *$1 << " (" << ((ExprTable *)it->second)->expressions.size()
			  << ") is too small to use " << *$2 << " (" << ir->second->ntokens() << ") as an index";
			yyerror(o.str().c_str());
		}
		// $1 is a map from string to Table*; $2 is a map from string to InsNameElem*
		$$ = new Binary(opExpTable, new Const($1->c_str()), new Const($2->c_str()));
		//delete $1;
		//delete $2;
	  }

	/* This is a "lambda" function-like parameter
	 * $1 is the "function" name, and $2 is a list of Exp* for the actual params.
	 * I believe only PA/RISC uses these so far. */
	| NAME_CALL list_actualparameter ')' {
		if (Dict.ParamSet.count(*$1)) {
			auto it = Dict.DetParamMap.find(*$1);
			if (it != Dict.DetParamMap.end()) {
				ParamEntry &param = it->second;
				if ($2->size() != param.funcParams.size()) {
					std::ostringstream o;
					o << *$1 << " requires " << param.funcParams.size() << " parameters, but received " << $2->size();
					yyerror(o.str().c_str());
					delete $1;
				} else {
					// Everything checks out. *phew*
					// Note: the below may not be right! (MVE)
					$$ = new Binary(opFlagDef, new Const($1->c_str()), listExpToExp($2));
					//delete $1;
				}
			} else {
				std::ostringstream o;
				o << *$1 << " is not defined as a OPERAND function";
				yyerror(o.str().c_str());
				delete $1;
			}
		} else {
			std::ostringstream o;
			o << "Unrecognized name " << *$1 << " in lambda call";
			yyerror(o.str().c_str());
			delete $1;
		}
		delete $2;
	  }

	| SUCCESSOR exp ')' { $$ = makeSuccessor($2); }
	;

exp
	: exp S_E { $$ = new Unary($2, $1); }

	/* "%prec CAST_OP" just says that this operator has the precedence of the dummy terminal CAST_OP
	 * It's a "precedence modifier" (see "Context-Dependent Precedence" in the Bison documantation) */
	| exp cast %prec CAST_OP {
		// size casts and the opSize operator were generally deprecated, but now opSize is used to transmit
		// the size of operands that could be memOfs from the decoder to type analysis
		if ($2 == STD_SIZE)
			$$ = $1;
		else
			$$ = new Binary(opSize, new Const($2), $1);
	  }

	| NOT  exp { $$ = new Unary($1, $2); }
	| LNOT exp { $$ = new Unary($1, $2); }
	| FNEG exp { $$ = new Unary($1, $2); }

	| exp FARITH_OP exp { $$ = new Binary($2, $1, $3); }
	| exp ARITH_OP  exp { $$ = new Binary($2, $1, $3); }
	| exp BIT_OP    exp { $$ = new Binary($2, $1, $3); }
	| exp COND_OP   exp { $$ = new Binary($2, $1, $3); }
	| exp LOG_OP    exp { $$ = new Binary($2, $1, $3); }

	/* See comment above re "%prec LOOKUP_RDC"
	 * Example: OP1[IDX] where OP1 := {  "&",  "|", "^", ... }; */
	| exp NAME_LOOKUP NAME ']' exp_term %prec LOOKUP_RDC {
		auto ir = indexrefmap.find(*$3);
		auto it = TableDict.find(*$2);
		if (ir == indexrefmap.end()) {
			std::ostringstream o;
			o << "Index " << *$3 << " not declared for use";
			yyerror(o.str().c_str());
		} else if (it == TableDict.end()) {
			std::ostringstream o;
			o << "Table " << *$2 << " not declared for use";
			yyerror(o.str().c_str());
		} else if (it->second->getType() != OPTABLE) {
			std::ostringstream o;
			o << "Table " << *$2 << " is not an operator table but appears to be used as one";
			yyerror(o.str().c_str());
		} else if ((int)((OpTable *)it->second)->operators.size() < ir->second->ntokens()) {
			std::ostringstream o;
			o << "Table " << *$2 << " is too small to use with " << *$3 << " as an index";
			yyerror(o.str().c_str());
		}
		$$ = new Ternary(opOpTable,
		                 new Const($2->c_str()),
		                 new Const($3->c_str()),
		                 new Binary(opList,
		                            $1,
		                            new Binary(opList,
		                                       $5,
		                                       new Terminal(opNil))));
		//delete $2;
		//delete $3;
	  }

	| exp_term { $$ = $1; }
	;

location
	/* This is for constant register numbers. Often, these are special, in the sense that the register mapping
	 * is -1. If so, the equivalent of a special register is generated, i.e. a Terminal or opMachFtr
	 * (machine specific feature) representing that register. */
	: REG_ID {
		bool isFlag = $1->find("flags") != $1->npos;
		auto it = Dict.RegMap.find(*$1);
		if (it == Dict.RegMap.cend() && !isFlag) {
			std::ostringstream o;
			o << "Register `" << *$1 << "' is undefined";
			yyerror(o.str().c_str());
			delete $1;
		} else if (isFlag || it->second == -1) {
			// A special register, e.g. %npc or %CF. Return a Terminal for it
			OPER op = strToTerm($1->c_str());
			if (op) {
				$$ = new Terminal(op);
				delete $1;
			} else {
				$$ = new Unary(opMachFtr,  // Machine specific feature
				               new Const($1->c_str()));
				//delete $1;
			}
		} else {
			// A register with a constant reg nmber, e.g. %g2.  In this case, we want to return r[const 2]
			$$ = Location::regOf(it->second);
			delete $1;
		}
	  }

	| REG_IDX exp ']' { $$ = Location::regOf($2); }
	| REG_NUM         { $$ = Location::regOf($1); }
	| MEM_IDX exp ']' { $$ = Location::memOf($2); }

	| NAME {
		// This is a mixture of the param: PARM {} match and the value_op: NAME {} match
		if (Dict.ParamSet.count(*$1)) {
			$$ = new Location(opParam, new Const($1->c_str()), nullptr);
			//delete $1;
		} else if (ConstTable.count(*$1)) {
			$$ = new Const(ConstTable[*$1]);
			delete $1;
		} else {
			std::ostringstream o;
			o << "`" << *$1 << "' is not a constant, definition or a parameter of this instruction";
			yyerror(o.str().c_str());
			$$ = new Const(0);
			delete $1;
		}
	  }

	| exp AT '[' exp ':' exp ']' { $$ = new Ternary(opAt, $1, $4, $6); }

	| TEMP { $$ = Location::tempOf(new Const($1->c_str())); /*delete $1;*/ }

	/* This indicates a post-instruction marker (var tick) */
	| location '\'' { $$ = new Unary(opPostVar, $1); }
	| SUCCESSOR exp ')' { $$ = makeSuccessor($2); }
	;

cast
	: '{' NUM '}' { $$ = $2; }
	;

endianness
	: ENDIANNESS esize { Dict.bigEndian = $2; }
	;

esize
	: BIG    { $$ = true; }
	| LITTLE { $$ = false; }
	;

assigntype
	: ASSIGNTYPE {
		const char *s = $1->c_str();
		char c = s[0];
		int size = 0;
		if (!c) {
			$$ = new SizeType(size); // MVE: should remove these
		} else if (isdigit(c)) {
			sscanf(s, "%d", &size);
			$$ = new SizeType(size);
		} else {
			// Skip letter
			sscanf(s + 1, "%d", &size);
			if (size == 0) size = STD_SIZE;
			switch (c) {
			case 'i': $$ = new IntegerType(size,  1); break;
			case 'j': $$ = new IntegerType(size,  0); break;
			case 'u': $$ = new IntegerType(size, -1); break;
			case 'f': $$ = new FloatType(size); break;
			case 'c': $$ = new CharType; break;
			default:
				std::cerr << "Unexpected char " << c << " in assign type\n";
				$$ = new IntegerType;
			}
		}
		delete $1;
	  }
	;

/* Section for indicating which instructions to substitute when using -f (fast but not quite as exact instruction mapping) */
fastlist
	: FAST fastentries
	;

fastentries
	: fastentries ',' fastentry
	| fastentry
	;

fastentry
	: NAME INDEX NAME { Dict.fastMap[*$1] = *$3; delete $1; delete $3; }
	;

%%

/*==============================================================================
 * FUNCTION:        SSLParser::parseExp
 * OVERVIEW:        Parses an assignment from a string.
 * PARAMETERS:      the string
 * RETURNS:         an Assignment or nullptr.
 *============================================================================*/
Statement *
SSLParser::parseExp(const char *str)
{
	std::istringstream ss(str);
	SSLParser p(ss, false);  // Second arg true for debugging
	RTLInstDict d;
	p.yyparse(d);
	return p.the_asgn;
}

/*==============================================================================
 * FUNCTION:        SSLParser::~SSLParser
 * OVERVIEW:        Destructor.
 *============================================================================*/
SSLParser::~SSLParser()
{
	delete theScanner;
	for (auto loc = TableDict.begin(); loc != TableDict.end(); ++loc)
		delete loc->second;
}

/*==============================================================================
 * FUNCTION:        SSLParser::yyerror
 * OVERVIEW:        Display an error message and exit.
 * PARAMETERS:      msg - an error message
 *============================================================================*/
void
SSLParser::yyerror(const char* msg)
{
	// TODO:  Replace "ssl" with the filename.
	std::cerr << "ssl:" << theScanner->theLine << ": " << msg << "\n";
}

/*==============================================================================
 * FUNCTION:        SSLParser::yylex
 * OVERVIEW:        The scanner driver than returns the next token.
 * RETURNS:         the next token
 *============================================================================*/
int
SSLParser::yylex()
{
	int token = theScanner->yylex(yylval);
	return token;
}

OPER
strToTerm(const char *s)
{
	// s could be %pc, %afp, %agp, %CF, %ZF, %OF, %NF, %DF, %flags, %fflags
	if (s[2] == 'F') {
		if (s[1] <= 'N') {
			if (s[1] == 'C') return opCF;
			if (s[1] == 'N') return opNF;
			return opDF;
		} else {
			if (s[1] == 'O') return opOF;
			return opZF;
		}
	}
	if (s[1] == 'p') return opPC;
	if (s[1] == 'a') {
		if (s[2] == 'f') return opAFP;
		if (s[2] == 'g') return opAGP;
	} else if (s[1] == 'f') {
		if (s[2] == 'l') return opFlags;
		if (s[2] == 'f') return opFflags;
	}
	return (OPER)0;
}

/*==============================================================================
 * FUNCTION:        listExpToExp
 * OVERVIEW:        Convert a list of actual parameters in the form of a STL list of Exps into one expression
 *                    (using opList)
 * NOTE:            The expressions in the list are not cloned; they are simply copied to the new opList
 * PARAMETERS:      le: the list of expressions
 * RETURNS:         The opList Expression
 *============================================================================*/
Exp *
listExpToExp(std::list<Exp *> *le)
{
	Exp *e = new Terminal(opNil);  // Terminate the chain
	for (auto it = le->rbegin(); it != le->rend(); ++it)
		e = new Binary(opList, *it, e);
	return e;
}

/*==============================================================================
 * FUNCTION:        listStrToExp
 * OVERVIEW:        Convert a list of formal parameters in the form of a STL list of strings into one expression
 *                    (using opList)
 * PARAMETERS:      ls - the list of strings
 * RETURNS:         The opList expression
 *============================================================================*/
Exp *
listStrToExp(std::list<std::string> *ls)
{
	Exp *e = new Terminal(opNil);  // Terminate the chain
	for (auto it = ls->rbegin(); it != ls->rend(); ++it)
		e = new Binary(opList, new Location(opParam, new Const(it->c_str()), nullptr), e);
	return e;
}

static Exp *srchExpr = new Binary(opExpTable,
                                  new Terminal(opWild),
                                  new Terminal(opWild));
static Exp *srchOp = new Ternary(opOpTable,
                                 new Terminal(opWild),
                                 new Terminal(opWild),
                                 new Terminal(opWild));
#ifdef GARBAGE_COLLECTOR
void
init_sslparser()
{
	static Exp **gc_pointers = (Exp **)GC_MALLOC_UNCOLLECTABLE(2 * sizeof *gc_pointers);
	gc_pointers[0] = srchExpr;
	gc_pointers[1] = srchOp;
}
#endif

/*==============================================================================
 * FUNCTION:        SSLParser::expandTables
 * OVERVIEW:        Expand tables in an RTL and save to dictionary
 * NOTE:            This may generate many entries
 * PARAMETERS:      iname: Parser object representing the instruction name
 *                  params: Parser object representing the instruction params
 *                  o_rtlist: Original rtlist object (before expanding)
 *                  Dict: Ref to the dictionary that will contain the results of the parse
 *============================================================================*/
void
SSLParser::expandTables(InsNameElem *iname, std::list<std::string> *params, RTL *o_rtlist, RTLInstDict &Dict)
{
	assert(iname->ninstructions());
	iname->reset();
	// Expand the tables (if any) in this instruction
	do {
		std::string nam = iname->getinstruction();
		// Need to make substitutions to a copy of the RTL
		RTL *rtl = o_rtlist->clone();
		int n = rtl->getNumStmt();
		for (int j = 0; j < n; ++j) {
			Statement *s = rtl->elementAt(j);
			std::list<Exp *> le;
			// Expression tables
			assert(s->getKind() == STMT_ASSIGN);
			if (((Assign *)s)->searchAll(srchExpr, le)) {
				for (auto it = le.begin(); it != le.end(); ++it) {
					const char *tbl = ((Const *)((Binary *)*it)->getSubExp1())->getStr();
					const char *idx = ((Const *)((Binary *)*it)->getSubExp2())->getStr();
					Exp *repl =((ExprTable *)(TableDict[tbl]))->expressions[indexrefmap[idx]->getvalue()];
					s->searchAndReplace(*it, repl);
				}
			}
			// Operator tables
			Exp *res;
			while (s->search(srchOp, res)) {
				Ternary *t;
				if (res->isTypedExp())
					t = (Ternary *)res->getSubExp1();
				else
					t = (Ternary *)res;
				assert(t->getOper() == opOpTable);
				// The ternary opOpTable has a table and index name as strings, then a list of 2 expressions
				// (and we want to replace it with e1 OP e2)
				const char *tbl = ((Const *)t->getSubExp1())->getStr();
				const char *idx = ((Const *)t->getSubExp2())->getStr();
				// The expressions to operate on are in the list
				Binary *b = (Binary *)t->getSubExp3();
				assert(b->getOper() == opList);
				Exp *e1 = b->getSubExp1();
				Exp *e2 = b->getSubExp2();  // This should be an opList too
				assert(e2->getOper() == opList);
				e2 = ((Binary *)e2)->getSubExp1();
				OPER ops = ((OpTable *)(TableDict[tbl]))->operators[indexrefmap[idx]->getvalue()];
				Exp *repl = new Binary(ops, e1->clone(), e2->clone());  // FIXME!
				s->searchAndReplace(res, repl);
			}
		}

		if (Dict.appendToDict(nam, *params, *rtl) != 0) {
			std::ostringstream o;
			o << "Pattern " << iname->getinspattern() << " conflicts with an earlier declaration of " << nam;
			yyerror(o.str().c_str());
		}
	} while (iname->increment());
}

/*==============================================================================
 * FUNCTION:        SSLParser::makeSuccessor
 * OVERVIEW:        Make the successor of the given expression, e.g. given r[2], return succ(r[2])
 *                    (using opSuccessor)
 *                  We can't do the successor operation here, because the parameters are not yet instantiated
 *                    (still of the form param(rd)). Actual successor done in Exp::fixSuccessor()
 * NOTE:            The given expression should be of the form  r[const]
 * NOTE:            The parameter expresion is copied (not cloned) in the result
 * PARAMETERS:      The expression to find the successor of
 * RETURNS:         The modified expression
 *============================================================================*/
Exp *
SSLParser::makeSuccessor(Exp *e)
{
	return new Unary(opSuccessor, e);
}
